apply plugin: "com.android.library"
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group maven.groupId
version maven.version

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
        }
    }

    externalNativeBuild {
        ndkBuild {
            setPath(new File(projectDir, "src/main/jni/Android.mk"))
        }
    }

    // release only
    variantFilter {
        if (buildType.name == "debug") {
            ignore = true
        }
    }
}

dependencies {
    compileOnly deps.kotlin
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from destinationDir
}

dokka {
    outputFormat = 'html'
    outputDirectory "$buildDir/javadoc"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name maven.name
            packaging 'aar'
            groupId maven.groupId
            artifactId maven.artifactId
            version maven.version
            url maven.siteUrl
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_apikey') ? bintray_apikey : ''

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = maven.name
        desc = maven.desc
        licenses = maven.licenses
        websiteUrl = maven.siteUrl
        vcsUrl = maven.gitUrl
        issueTrackerUrl = maven.issueTrackerUrl
        githubRepo = maven.githubRepo
        labels = maven.labels
        publicDownloadNumbers = true
        version {
            name = maven.version
            released = new Date()
            gpg {
                sign = true
                passphrase = project.hasProperty('bintray_gpg_password') ? bintray_gpg_password : ''
            }
        }
    }
}
